apply plugin: 'java'
apply plugin: 'maven'

ext.artemis_version = "1.0.1"
ext.gwt_version = "2.5.0"

group = "com.artemis"
version = artemis_version

sourceSets {
	emulated
	artemisemulated
}

repositories {
	mavenCentral()
}

uploadArchives {
        repositories {
                mavenDeployer {
                        repository(url: "https://maven.visualdna.com/nexus/content/repositories/releases/")
                        snapshotRepository(url: "https://maven.visualdna.com/nexus/content/repositories/snapshots/")
                }
        }
}

dependencies {
	compile group: "com.google.gwt", name: "gwt-user", version: gwt_version
}

jar {
	from sourceSets.main.allJava
	from sourceSets.emulated.allJava
	from sourceSets.artemisemulated.allJava
}

task distZip(dependsOn: ["jar"], type: Zip) {
	from jar.archivePath
	from files("lib/artemis-" + artemis_version + ".jar")
}

build.dependsOn distZip
uploadArchives.dependsOn build

/**
 * Unpacks the artemis sources into the artemis source folder.
 * NOTE: Do call this only if you are able to replay the steps which where necessary to make this library gwt compatible.
 */ 
task unpackArtemis << {
	copy {
		from zipTree("lib/artemis-" + artemis_version + "-sources.jar")
		into "src/artemisemulated/java/com/artemis/"
	}
}
